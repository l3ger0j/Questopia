plugins {
    id 'com.android.application'
}

def viewBind = true
def dataBind = true

android {
    compileSdk 34

    defaultConfig {
        applicationId "org.qp.android"
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 202204
        versionName "3.22.4"
        resourceConfigurations += ['en', 'ru']
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    configurations {
        configureEach {
            exclude group: 'xpp3', module: 'xpp3'
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
        }
    }

    signingConfigs {
        release {
            storeFile file("keystore/github_keystore")
            storePassword System.getenv('KEYSTORE_PASSWORD')
            keyAlias System.getenv('RELEASE_SIGN_KEY_ALIAS')
            keyPassword System.getenv('RELEASE_SIGN_KEY_PASSWORD')
        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding viewBind
        dataBinding dataBind
        buildConfig true
        aidl true
    }

    ndkVersion = '26.1.10909125'
    lint {
        abortOnError false
    }

    namespace 'org.qp.android'
}

dependencies {
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.0.4")

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.fragment:fragment:1.6.2'
    implementation 'androidx.webkit:webkit:1.10.0'
    implementation 'androidx.navigation:navigation-fragment:2.7.7'
    implementation 'androidx.navigation:navigation-ui:2.7.7'
    implementation 'androidx.work:work-runtime:2.9.0'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.core:core-splashscreen:1.0.1'

    implementation 'com.github.AppDevNext:ChangeLog:3.5'
    implementation 'com.jaredrummler:colorpicker:1.1.0'
    implementation 'org.jsoup:jsoup:1.17.1'
    implementation 'com.github.javiersantos:AppUpdater:2.7'
    implementation 'com.anggrayudi:storage:1.5.5'
    implementation 'com.squareup.picasso:picasso:2.71828'

    implementation 'io.reactivex.rxjava3:rxandroid:3.0.2'

    implementation 'com.fasterxml.jackson.core:jackson-core:2.16.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.16.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
}
